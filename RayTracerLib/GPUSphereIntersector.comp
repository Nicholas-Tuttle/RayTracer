// glslangValidator.exe -V -e main %(FullPath) -o $(OutDir)\..\bin\%(Filename).comp.spv

#version 450
#extension GL_EXT_debug_printf : enable

struct Ray
{
	vec4 origin;
	vec4 direction;
	uint pixelXIndex;
	uint pixelYIndex;
};

layout(std430, set = 0, binding = 0) buffer in_rays
{
	Ray rays[];
} Rays;

struct Intersection
{
	vec4 ray_origin;
	vec4 ray_direction;
	vec4 intersection_location;
	vec4 intersection_normal;
	vec4 ray_color;
	float depth;
	uint object_id;
	uint face_id;
	uint material_id;
	int material_index;
};

layout(std430, set = 0, binding = 1) buffer out_intersections
{
	Intersection intersections[];
} Intersections;

struct Sphere
{
	vec4 position;
	float radius;
	uint material_id;
	int material_index;
};

layout(std430, set = 0, binding = 2) buffer in_spheres
{
	Sphere spheres[];
} Spheres;

struct RayIntersectionInfo
{
	bool intersects;
	float depth;
};

RayIntersectionInfo ray_intersects_sphere(vec3 ray_origin, vec3 ray_direction_normalized, vec3 sphere_position, float sphere_radius)
{
	RayIntersectionInfo info;
	info.intersects = false;

	vec3 line_origin_to_sphere_center_O_minus_C = sphere_position - ray_origin;
	float u_dot = dot(line_origin_to_sphere_center_O_minus_C, ray_direction_normalized);
	float dot_squared = u_dot * u_dot;
	float magnitude_squared = dot(line_origin_to_sphere_center_O_minus_C, line_origin_to_sphere_center_O_minus_C);
	float radius_squared = sphere_radius * sphere_radius;
	float delta = dot_squared + radius_squared - magnitude_squared;
	
	if (delta < 0)
	{
		return info;
	}

	// The closest depth has to be the "minus" term of the "plus or minus" square-root since delta is always positive
	float depth = u_dot - sqrt(delta);
	
	// If the depth is negative either only the backwards ray intersects, or it is a backface from starting in the sphere
	if (depth < 0)
	{
		return info;
	}

	info.intersects = true;
	info.depth = depth;

	return info;
}

layout(local_size_x = 1024, local_size_y = 1, local_size_z = 1) in;
void main()
{
	uint gID = gl_GlobalInvocationID.x;

#ifdef DEBUG
	if(gID == 0)
	{
		debugPrintfEXT("Sphere Intersection: %u, %u, %u", Rays.rays.length(), Intersections.intersections.length(), Spheres.spheres.length());
		if(Spheres.spheres.length() > 0)
		{
			debugPrintfEXT("Sphere 0 data: [%3.2v4f], %f, %u, %u", Spheres.spheres[0].position, Spheres.spheres[0].radius, Spheres.spheres[0].material_id, Spheres.spheres[0].material_index);
		}
	}
#endif
	
	if (gID > Rays.rays.length())
	{
		return;
	}
	
	vec3 ray_origin = Rays.rays[gID].origin.xyz;
	vec3 ray_direction_normalized = normalize(Rays.rays[gID].direction.xyz);

	for(int i = 0; i < Spheres.spheres.length(); i++)
	{
		RayIntersectionInfo info = ray_intersects_sphere(ray_origin, ray_direction_normalized, Spheres.spheres[i].position.xyz, Spheres.spheres[i].radius);
		
		if(info.intersects && info.depth < Intersections.intersections[gID].depth)
		{
			vec3 intersection_location = ray_origin + ray_direction_normalized * info.depth;
			vec3 intersection_normal = normalize((intersection_location - Spheres.spheres[i].position.xyz));

			// Fill in all the intersection info
			Intersections.intersections[gID].depth = info.depth;
			Intersections.intersections[gID].intersection_location = vec4(intersection_location, 0.0);
			Intersections.intersections[gID].intersection_normal = vec4(intersection_normal, 0.0);
			Intersections.intersections[gID].material_id = Spheres.spheres[i].material_id;
			Intersections.intersections[gID].material_index = Spheres.spheres[i].material_index;
		}
	}
}
